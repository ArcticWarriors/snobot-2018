
apply plugin: 'java'
apply from: "$rootDir/shared_styleguide.gradle"

repositories {
    mavenCentral()
    maven {
        url = "http://first.wpi.edu/FRC/roborio/maven/release"
    }
}

sourceSets {
    main {
        java {
            srcDirs = ["src", 
                       "../2018NetworkTableViewer/src", 
                       "../SmartDashboardLibs/src/main/java", 
                       "../CoordinateViewerGui/src/BaseYearSpecificGui", 
                       "../CoordinateViewerGui/src/2018Gui", 
                       "../CoordinateViewerGui/src/lib"]
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

jar {
   from("../CoordinateViewerGui/src/2018Gui/") {
      include  "**/*.png"
   }
   from("../CoordinateViewerGui/src/lib/") {
      include  "**/*.png"
   }
   from("src/") {
      include  "**/*.properties"
   }
}

task install_depencies_to_plugin_dir(type: Copy) {
    from configurations.compile {
        include "log4j*.jar"
    }
    into System.getProperty("user.home") + "/SmartDashboard/extensions"
}

task install_to_plugin_dir(type: Copy, dependsOn: install_depencies_to_plugin_dir) {
    group = "Network Table GUI"
    description ="Installs the jar to the plugin area"
    
    from jar.outputs
    into System.getProperty("user.home") + "/SmartDashboard/extensions"
}

dependencies {
    compile 'jfree:jfreechart:1.0.13'
    compile 'org.yaml:snakeyaml:1.18'
    compile 'org.apache.logging.log4j:log4j-api:2.11.0'
    compile 'org.apache.logging.log4j:log4j-core:2.11.0'
    compile 'log4j:log4j:1.2.16'
    
    compile 'edu.wpi.first.wpilib:SmartDashboard:3.0.0'
}